{
  config,
  pkgs,
  lib,
  nix-csi,
  ...
}:
let
  cfg = config.nix-csi;
  nsRes = config.kubernetes.resources.${cfg.namespace};
in
{
  options.nix-csi.cache = {
    storageClassName = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
    };
    loadBalancerPort = lib.mkOption {
      type = lib.types.int;
      default = 2222;
    };
  };
  config =
    let
      labels = {
        "app.kubernetes.io/name" = "cache";
        "app.kubernetes.io/part-of" = "nix-csi";
      };
    in
    lib.mkIf cfg.enable {
      kubernetes.resources.${cfg.namespace} = {
        ConfigMap.authorized-keys.data.authorized_keys = lib.concatLines cfg.authorizedKeys;
        StatefulSet.nix-cache = {
          spec = {
            serviceName = "nix-cache";
            replicas = 1;
            selector.matchLabels = labels;
            template = {
              metadata.labels = labels;
              metadata.annotations = {
                "kubectl.kubernetes.io/default-container" = "nix-cache";
                configHash = lib.hashAttrs nsRes.ConfigMap.nix-cache-config;
              };
              spec = {
                serviceAccountName = "nix-csi";
                initContainers = lib.mkNumberedList {
                  "1" = {
                    name = "initcopy";
                    image = "ghcr.io/lillecarl/nix-csi/lix:${pkgs.lix.version}";
                    imagePullPolicy = "Always";
                    securityContext.privileged = true; # chroot store
                    env =
                      lib.mkNamedList {
                        TAG.value = cfg.version;
                        amd64.value = nix-csi.x86_64-linux;
                        arm64.value = nix-csi.aarch64-linux;
                      }
                      // lib.optionalAttrs (lib.stringLength (builtins.getEnv "GITHUB_KEY") > 0) {
                        NIX_CONFIG.value = "access-tokens = github.com=${builtins.getEnv "GITHUB_KEY"}";
                      };
                    volumeMounts = lib.mkNamedList {
                      nix-store.mountPath = "/nix-volume";
                      nix-config.mountPath = "/etc/nix";
                    };
                  };
                };
                containers = lib.mkNamedList {
                  cache = {
                    command = [
                      "dinit"
                      "--log-file"
                      "/var/log/dinit.log"
                      "--quiet"
                      "cache"
                    ];
                    image = "ghcr.io/lillecarl/nix-csi/scratch:1.0.1";
                    env = lib.mkNamedList {
                      HOME.value = "/nix/var/nix-csi/root";
                      KUBE_NAMESPACE.valueFrom.fieldRef.fieldPath = "metadata.namespace";
                      BUILDERS_SERVICE_NAME.value = cfg.internalServiceName;
                    };
                    ports = lib.mkNamedList {
                      ssh.containerPort = 22;
                      http.containerPort = 80;
                    };
                    volumeMounts = lib.mkNamedList {
                      nix-config.mountPath = "/etc/nix-mount";
                      ssh.mountPath = "/etc/ssh-mount";
                      nix-store = {
                        mountPath = "/nix";
                        subPath = "nix";
                      };
                    };
                  };
                };
                volumes = lib.mkNamedList {
                  nix-config.configMap.name = "nix-cache-config";
                  ssh.secret = {
                    secretName = "ssh";
                    defaultMode = 384;
                    optional = true;
                  };
                };
              };
            };
            volumeClaimTemplates = lib.mkNumberedList {
              "1" = {
                metadata.name = "nix-store";
                spec = {
                  accessModes = [ "ReadWriteOnce" ];
                  resources.requests.storage = "10Gi";
                  inherit (cfg.cache) storageClassName;
                };
              };
            };
          };
        };

        Service.nix-cache = {
          spec = {
            selector = labels;
            ports = lib.mkNamedList {
              ssh = {
                port = 22;
                targetPort = "ssh";
              };
            };
            type = "ClusterIP";
          };
        };
        Service.nix-cache-lb = {
          spec = {
            selector = labels;
            ports = lib.mkNamedList {
              ssh = {
                port = cfg.cache.loadBalancerPort;
                targetPort = "ssh";
              };
            };
            type = "LoadBalancer";
          };
        };
      };
    };
}
